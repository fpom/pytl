%import common.WS
%ignore WS

start : phi (BOOL phi)* (FAIR phi (BOOL phi)*)?

phi : sub                                            -> nop
    | QUAN [act] (QMOD [act])* sub [BMOD [act] sub]  -> mod

sub : "(" phi (BOOL phi)* ")"  -> bin_op
    | "~" sub                  -> not_op
    | ATOM                     -> atom
    
act : "{" exp (BOOL exp)* "}"  -> bin_op

exp : "(" exp (BOOL exp)* ")"  -> bin_op
    | "~" exp                  -> not_op
    | ATOM                     -> atom

FAIR.3 : /\bFAIR\b/
QMOD.2 : /\b[AEXFG]+\b/
QUAN.1 : /\b[AE][AEXFG]*\b/
BMOD.1 : /\b[UR]\b/
ATOM.0 : /\b\w+\b|"[^"]+"|'[^']+'/
BOOL.0 : /&|\||=>|<=>/
