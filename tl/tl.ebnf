%import common.WS
%ignore WS

start : phi [FAIR phi]

phi : ATOM                     -> atom
    | "(" "~" phi ")"          -> not_op
    | "(" phi BOOL phi ")"     -> bin_op
    | QUAN [actions] path      -> unary_mod

path : phi                     -> nop
    | "(" "~" path ")"         -> not_op
    | "(" path BOOL path ")"   -> bin_op
    | UMOD [actions] path      -> unary_mod
    | path BMOD [actions] path -> binary_mod

actions : "{" act_expr "}"     -> nop

act_expr : "(" act_expr ")"    -> nop
	| "~" act_expr             -> not_op
    | act_expr BOOL act_expr   -> bin_op
	| ATOM                     -> atom

FAIR.2 : "FAIR"
QUAN.1 : /[AE][XFG]?/
UMOD.1 : /[XFG]/
BMOD.1 : /[UR]/
ATOM.0 : /\b\w+\b|"[^"]+"|'[^']+'/
BOOL.0 : /&|\||=>|<=>/
